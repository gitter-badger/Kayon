/*
 * Kayon
 * Copyright (C) 2015 Ruben Anders
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import groovy.json.JsonSlurper
import org.eclipse.jgit.api.Git
import org.eclipse.jgit.storage.file.FileRepositoryBuilder
import org.eclipse.jgit.transport.UsernamePasswordCredentialsProvider

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

apply from: 'javadocAndSourcesJar.gradle'

group 'cf.kayon'
version = file('src/main/resources/version').getText('utf8').replaceAll("[\n\r]", "") // eliminate newlines
mainClassName = 'cf.kayon.gui.splash.Splash'

if (Objects.equals(System.getenv('CIRCLECI'), 'true')) {
    file('src/main/resources/build').write(System.getenv('CIRCLE_BUILD_NUM'))

    def versionForFiles = project.version + '-b' + System.getenv('CIRCLE_BUILD_NUM')

    tasks.withType(Jar) {
        version = versionForFiles
    }
    tasks.withType(Zip) {
        version = versionForFiles
    }
    tasks.withType(Tar) {
        version = versionForFiles
    }

    task copyCIArtifacts << { // because why not, doubleshift and nested copy (intuitive, isn't it?)
        copy {
            from(file('build/libs/').listFiles() + file('build/distributions/').listFiles())
            into System.getenv('CIRCLE_ARTIFACTS')
        }
    }

    task deployJavadoc(dependsOn: tasks.javadoc) << { // double shift because closure is what is running the task, not configuring it
        if (!Objects.equals(System.getenv('CIRCLE_BRANCH'), 'master')) // only do javadoc pushes on commits to `master`
            return;
        def directory = file("$buildDir/doc-repo/")
        def git = null;
        try {
//            git = Git.cloneRepository().setBranch('gh-pages').setURI('https://github.com/RAnders00/KayonDoc.git').setDirectory(directory).call()
            directory.mkdirs()
            ProcessBuilder builder = new ProcessBuilder('git', 'clone', '-b', 'gh-pages', 'https://github.com/RAnders00/KayonDoc.git', '.')
            builder.directory(directory)
            Process process = builder.start()
            process.waitFor()

            git = new Git(new FileRepositoryBuilder().setGitDir(file(directory.absolutePath + '/.git')).readEnvironment().findGitDir().build())
            def rm = git.rm()
            directory.list().each {
                if (!it.equals('README.md'))
                    rm.addFilepattern(it) // windows
            }
            rm.call()

            copy {
                from tasks.javadoc.destinationDir
                into directory
            }
            git.add().addFilepattern('.').call()
            def json = new JsonSlurper().parseText(new URL("https://api.github.com/repos/RAnders00/Kayon/git/commits/${System.getenv('CIRCLE_SHA1')}").text)
            def message = "[#${System.getenv("CIRCLE_BUILD_NUM")}] $json.message\nBuild URL: https://circleci.com/gh/RAnders00/Kayon/${System.getenv("CIRCLE_BUILD_NUM")}"
            git.commit().setAuthor(json.author.name, json.author.email).setCommitter('KayonBot', 'rubenanders@hotmail.de').setMessage(message).call()
            git.push().setPushAll().setCredentialsProvider(new UsernamePasswordCredentialsProvider('KayonBot', System.getenv('KAYONBOT_PASSWORD'))).call()
        } catch (Throwable thr) {
            thr.printStackTrace()
        } finally {
            if (git != null)
                git.close()
        }
    }
}

buildscript {
    repositories {
        jcenter()
        maven { url 'https://repo.eclipse.org/content/groups/releases/' }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath 'org.eclipse.jgit:org.eclipse.jgit:4.1.1.201511131810-r'
    }
}

//region Dependency Management
repositories {
    maven { url "http://dl.bintray.com/randers00/intellijannotations" }
    jcenter()
}

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.apache.commons:commons-collections4:4.0' // ObjectArrayIterator
    compile 'com.intellij:annotations:15.0'
    compile "com.google.guava:guava:18.0" // Change version in doc link as well
    compile 'org.apache.commons:commons-lang3:3.4' // Pair & Triple
    compile 'commons-io:commons-io:2.4'
    compile 'com.h2database:h2:1.4.190'
//    compile fileTree(dir: 'libs', include: '*.jar')

    // Tests
    testCompile 'junit:junit:4.7'
    testCompile 'com.github.stefanbirkner:fishbowl:1.3.0'
}
//endregion

//region utf8
tasks.withType(JavaCompile) {
    options.encoding = 'utf8'
}

tasks.run {
    workingDir "$buildDir/run-dir/"
    doFirst {
        workingDir.mkdirs()
    }
}

String buildOptionsString(String... links) {
    StringBuilder stringBuilder = new StringBuilder(links.length * 32)
    for (int i = 0; i < links.length; i++) {
        stringBuilder.append(links[i])
        if (i + 1 < links.length)
            stringBuilder.append("' -link '")
    }
    return stringBuilder.toString()
}

tasks.javadoc {
    options.addStringOption('link', buildOptionsString('https://cdn.rawgit.com/google/guava/036db99cc2d8cee83de0f07ca0664b522fbbeaca/_releases/18.0/api/docs/',
            'https://docs.oracle.com/javase/8/docs/api/'))
    options.addStringOption('encoding', 'utf8')
    options.addStringOption('docencoding', 'utf8')
    options.addStringOption('charset', 'utf8')
    options.showFromPrivate()
}
//endregion

tasks.withType(Test) {
    workingDir = "$buildDir/test-results/working-dir"
    systemProperty 'file.encoding', 'utf8'
    doFirst { workingDir.mkdirs() }
}

defaultTasks 'clean', 'jar', 'javadocJar', 'sourcesJar', 'shadowJar', 'distZip'
