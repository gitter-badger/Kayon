/*
 * Kayon
 * Copyright (C) 2015 Ruben Anders
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'edu.sc.seis.launch4j'

mainClassName = 'cf.kayon.gui.splash.Splash'

buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath 'gradle.plugin.edu.sc.seis.gradle:launch4j:1.6.1'
    }
}

dependencies {
    compile 'de.jensd:fontawesomefx:8.7'
    compile project(':KayonCore')
}


shadowJar {
    exclude 'META-INF/LICENSE.txt' // don't include foreign incorrect licenses
    exclude 'META-INF/NOTICE.txt'
}

launch4j {
    if (Objects.equals(System.getenv('CIRCLECI'), 'true'))
        outfile = "$project.name-$project.version-b${System.getenv('CIRCLE_BUILD_NUM')}-all.exe"
    else
        outfile = "$project.name-${project.version}-all.exe"
    mainClassName = project.mainClassName
    icon = file('logo/logo.ico')
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
}
tasks.copyL4jLib.dependsOn tasks.shadowJar

tasks.run {
    workingDir "$buildDir/test-results/working-dir/"
    doFirst { workingDir.mkdirs() }
}
